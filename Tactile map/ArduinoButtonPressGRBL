// Pin setup
const int buttonNextPin = 2;
const int buttonPrevPin = 3;

const int redPin = 9;
const int greenPin = 10;
const int bluePin = 11;

String colors[] = {"red", "blue", "green", "yellow", "orange", "purple", "black", "white", "pink"};
int currentColorIndex = 0;

unsigned long lastDebounce = 0;
const unsigned long debounceDelay = 250;

void setup() {
  pinMode(buttonNextPin, INPUT_PULLUP);
  pinMode(buttonPrevPin, INPUT_PULLUP);
  
  pinMode(redPin, OUTPUT);
  pinMode(greenPin, OUTPUT);
  pinMode(bluePin, OUTPUT);

  Serial.begin(115200);
  delay(1000);
  Serial.println("Ready");
  updateLED();
}

void loop() {
  // Debounce
  if (millis() - lastDebounce > debounceDelay) {
    bool nextPressed = digitalRead(buttonNextPin) == LOW;
    bool prevPressed = digitalRead(buttonPrevPin) == LOW;

    if (nextPressed && prevPressed) {
      // Both buttons pressed = CONFIRM COLOR
      Serial.print("$COLOR=");
      Serial.println(colors[currentColorIndex]);
      delay(500); // Delay for confirmation feedback
    } else if (nextPressed) {
      currentColorIndex = (currentColorIndex + 1) % (sizeof(colors)/sizeof(colors[0]));
      Serial.println("$NEXT");
      updateLED();
      lastDebounce = millis();
    } else if (prevPressed) {
      currentColorIndex = (currentColorIndex - 1 + (sizeof(colors)/sizeof(colors[0]))) % (sizeof(colors)/sizeof(colors[0]));
      Serial.println("$PREV");
      updateLED();
      lastDebounce = millis();
    }
  }

  // Listen for color command from Python
  if (Serial.available()) {
    String input = Serial.readStringUntil('\n');
    input.trim();
    if (input.startsWith("$COLOR=")) {
      String color = input.substring(7);
      for (int i = 0; i < sizeof(colors)/sizeof(colors[0]); i++) {
        if (color == colors[i]) {
          currentColorIndex = i;
          updateLED();
          break;
        }
      }
    }
  }
}

void updateLED() {
  String color = colors[currentColorIndex];
  if (color == "red") setLED(255, 0, 0);
  else if (color == "blue") setLED(0, 0, 255);
  else if (color == "green") setLED(0, 255, 0);
  else if (color == "yellow") setLED(255, 255, 0);
  else if (color == "orange") setLED(255, 165, 0);
  else if (color == "purple") setLED(128, 0, 128);
  else if (color == "black") setLED(0, 0, 0);
  else if (color == "white") setLED(255, 255, 255);
  else if (color == "pink") setLED(255, 105, 180);
}

void setLED(int r, int g, int b) {
  analogWrite(redPin, r);
  analogWrite(greenPin, g);
  analogWrite(bluePin, b);
}
